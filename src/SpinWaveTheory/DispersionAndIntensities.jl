# Bogoliubov transformation that diagonalizes a quadratic bosonic Hamiltonian,
# allowing for anomalous terms. The general procedure derives from Colpa,
# Physica A, 93A, 327-353 (1978). Overwrites data in H.
function bogoliubov!(T::Matrix{ComplexF64}, H::Matrix{ComplexF64})
    L = div(size(H, 1), 2)
    @assert size(T) == size(H) == (2L, 2L)
    # H0 = copy(H)

    # Initialize T to the para-unitary identity IÌƒ = diagm([ones(L), -ones(L)])
    T .= 0
    for i in 1:L
        T[i, i] = 1
        T[i+L, i+L] = -1
    end

    # Solve generalized eigenvalue problem, IÌƒ t = Î» H t, for columns t of T.
    # Eigenvalues are sorted such that positive values appear first, and are
    # otherwise ascending in absolute value.
    sortby(x) = (-sign(x), abs(x))
    Î», T0 = eigen!(Hermitian(T), Hermitian(H); sortby)

    # Note that T0 and T refer to the same data.
    @assert T0 === T

    # Normalize columns of T so that para-unitarity holds, Tâ€  IÌƒ T = IÌƒ.
    for j in axes(T, 2)
        c = 1 / sqrt(abs(Î»[j]))
        view(T, :, j) .*= c
    end

    # Inverse of Î» are eigenvalues of IÌƒ H, or equivalently, of âˆšH IÌƒ âˆšH.
    energies = Î»        # reuse storage
    @. energies = 1 / Î»

    # By Sylvester's theorem, "inertia" (sign signature) is invariant under a
    # congruence transform IÌƒ â†’ âˆšH IÌƒ âˆšH. The first L elements are positive,
    # while the next L elements are negative. Their absolute values are
    # excitation energies for the wavevectors q and -q, respectively.
    @assert all(>(0), view(energies, 1:L)) && all(<(0), view(energies, L+1:2L))

    # Disable tests below for speed. Note that the data in H has been
    # overwritten by eigen!, so H0 should refer to an original copy of H.
    #=
    IÌƒ = Diagonal([ones(L); -ones(L)])
    @assert T' * IÌƒ * T â‰ˆ IÌƒ
    @assert diag(T' * H0 * T) â‰ˆ IÌƒ * energies
    # Reflection symmetry H(q) = H(-q) is identified as H11 = conj(H22). In this
    # case, eigenvalues come in pairs.
    if H0[1:L, 1:L] â‰ˆ conj(H0[L+1:2L, L+1:2L])
        @assert energies[1:L] â‰ˆ -energies[L+1:2L]
    end
    =#

    return energies
end


# Returns |1 + nB(Ï‰)| where nB(Ï‰) = 1 / (exp(Î²Ï‰) - 1) is the Bose function.
# Equivalent to |1 / expm1(-Î²Ï‰)| where expm1(x) = e^x-1.
function thermal_prefactor(Ï‰; kT)
    @assert kT >= 0
    iszero(Ï‰) && return Inf
    return abs(1 / expm1(-Ï‰/kT))
end


"""
    excitations!(T, tmp, swt::SpinWaveTheory, q)

Given a wavevector `q`, solves for the matrix `T` representing quasi-particle
excitations, and returns a list of quasi-particle energies. Both `T` and `tmp`
must be supplied as ``2LÃ—2L`` complex matrices, where ``L`` is the number of
bands for a single ``ðª`` value.

The columns of `T` are understood to be contracted with the Holstein-Primakoff
bosons ``[ð›_ðª, ð›_{-ðª}^â€ ]``. The first ``L`` columns provide the eigenvectors
of the quadratic Hamiltonian for the wavevector ``ðª``. The next ``L`` columns
of `T` describe eigenvectors for ``-ðª``. The return value is a vector with
similar grouping: the first ``L`` values are energies for ``ðª``, and the next
``L`` values are the _negation_ of energies for ``-ðª``.

    excitations!(T, tmp, swt::SpinWaveTheorySpiral, q; branch)

Calculations on a [`SpinWaveTheorySpiral`](@ref) additionally require a `branch`
index. The possible branches ``(1, 2, 3)`` correspond to scattering processes
``ðª - ð¤, ðª, ðª + ð¤`` respectively, where ``ð¤`` is the ordering wavevector.
Each branch will contribute ``L`` excitations, where ``L`` is the number of
spins in the magnetic cell. This yields a total of ``3L`` excitations for a
given momentum transfer ``ðª``.
"""
function excitations!(T, tmp, swt::SpinWaveTheory, q)
    L = nbands(swt)
    size(T) == size(tmp) == (2L, 2L) || error("Arguments T and tmp must be $(2L)Ã—$(2L) matrices")

    q_reshaped = to_reshaped_rlu(swt.sys, q)
    dynamical_matrix!(tmp, swt, q_reshaped)

    try
        return bogoliubov!(T, tmp)
    catch _
        rethrow(ErrorException("Not an energy-minimum; wavevector q = $q unstable."))
    end
end

"""
    excitations(swt::SpinWaveTheory, q)
    excitations(swt::SpinWaveTheorySpiral, q; branch)

Returns a pair `(energies, T)` providing the excitation energies and
eigenvectors. Prefer [`excitations!`](@ref) for performance, which avoids matrix
allocations. See the documentation of [`excitations!`](@ref) for more details.
"""
function excitations(swt::SpinWaveTheory, q)
    L = nbands(swt)
    T = zeros(ComplexF64, 2L, 2L)
    H = zeros(ComplexF64, 2L, 2L)
    energies = excitations!(T, copy(H), swt, q)
    return (energies, T)
end

"""
    dispersion(swt::SpinWaveTheory, qpts)

Given a list of wavevectors `qpts` in reciprocal lattice units (RLU), returns
excitation energies for each band. The return value `ret` is 2D array, and
should be indexed as `ret[band_index, q_index]`.
"""
function dispersion(swt::SpinWaveTheory, qpts)
    L = nbands(swt)
    qpts = convert(AbstractQPoints, qpts)
    disp = zeros(L, length(qpts.qs))
    for (iq, q) in enumerate(qpts.qs)
        view(disp, :, iq) .= view(excitations(swt, q)[1], 1:L)
    end
    return reshape(disp, L, size(qpts.qs)...)
end

"""
    intensities_bands(swt::SpinWaveTheory, qpts; kT=0)

Calculate spin wave excitation bands for a set of q-points in reciprocal space.
This calculation is analogous to [`intensities`](@ref), but does not perform
line broadening of the bands.
"""
function intensities_bands(swt::SpinWaveTheory, qpts; kT=0, with_negative=false)
    (; sys, measure) = swt
    isempty(measure.observables) && error("No observables! Construct SpinWaveTheory with a `measure` argument.")
    with_negative && error("Option `with_negative=true` not yet supported.")

    qpts = convert(AbstractQPoints, qpts)
    cryst = orig_crystal(sys)

    # Number of (magnetic) atoms in magnetic cell
    @assert sys.dims == (1,1,1)
    Na = nsites(sys)
    # Number of chemical cells in magnetic cell
    Ncells = Na / natoms(cryst)
    # Number of quasiparticle modes
    L = nbands(swt)
    # Number of wavevectors
    Nq = length(qpts.qs)

    # Temporary storage for pair correlations
    Nobs = num_observables(measure)
    Ncorr = num_correlations(measure)
    corrbuf = zeros(ComplexF64, Ncorr)

    # Preallocation
    T = zeros(ComplexF64, 2L, 2L, Nq)
    H = zeros(ComplexF64, 2L, 2L, Nq)
    Avec_pref = zeros(ComplexF64, Nobs, Na)
    disp = zeros(Float64, L, Nq)
    intensity = zeros(eltype(measure), L, Nq)

    for (iq, q) in enumerate(qpts.qs)
        q_reshaped = to_reshaped_rlu(swt.sys, q)
        dynamical_matrix!(view(H, :, :, iq), swt, q_reshaped)
        for i in 1:L
            T[i, i, iq] = 1
            T[i+L, i+L, iq] = -1
        end
    end

    for (iq, q) in enumerate(qpts.qs)
        Hq = view(H,:,:,iq)
        Tq = view(T,:,:,iq)

        # Solve generalized eigenvalue problem, IÌƒ t = Î» H t, for columns t of T.
        # Eigenvalues are sorted such that positive values appear first, and are
        # otherwise ascending in absolute value.
        sortby(x) = (-sign(x), abs(x))
        Î», T0 = eigen!(Hermitian(Tq), Hermitian(Hq); sortby)

        # Note that T0 and T refer to the same data.
        @assert T0 === Tq

        # Normalize columns of T so that para-unitarity holds, Tâ€  IÌƒ T = IÌƒ.
        for j in axes(Tq, 2)
            c = 1 / sqrt(abs(Î»[j]))
            view(Tq, :, j) .*= c
        end

        # Inverse of Î» are eigenvalues of IÌƒ H, or equivalently, of âˆšH IÌƒ âˆšH.
        energies = Î»        # reuse storage
        @. energies = 1 / Î»

        # By Sylvester's theorem, "inertia" (sign signature) is invariant under a
        # congruence transform IÌƒ â†’ âˆšH IÌƒ âˆšH. The first L elements are positive,
        # while the next L elements are negative. Their absolute values are
        # excitation energies for the wavevectors q and -q, respectively.
        @assert all(>(0), view(energies, 1:L)) && all(<(0), view(energies, L+1:2L))

        view(disp, :, iq) .= view(energies, 1:L)
    end

    for (iq, q) in enumerate(qpts.qs)
        q_global = cryst.recipvecs * q
        Tq = view(T,:,:,iq)
        for i in 1:Na, Î¼ in 1:Nobs
            r_global = global_position(sys, (1, 1, 1, i)) # + offsets[Î¼, i]
            ff = get_swt_formfactor(measure, Î¼, i)
            Avec_pref[Î¼, i] = exp(- im * dot(q_global, r_global))
            Avec_pref[Î¼, i] *= compute_form_factor(ff, norm2(q_global))
        end

        Avec = zeros(ComplexF64, Nobs)

        # Fill `intensity` array
        for band in 1:L
            fill!(Avec, 0)
            if sys.mode == :SUN
                data = swt.data::SWTDataSUN
                N = sys.Ns[1]
                t = reshape(view(Tq, :, band), N-1, Na, 2)
                for i in 1:Na, Î¼ in 1:Nobs
                    O = data.observables_localized[Î¼, i]
                    for Î± in 1:N-1
                        Avec[Î¼] += Avec_pref[Î¼, i] * (O[Î±, N] * t[Î±, i, 2] + O[N, Î±] * t[Î±, i, 1])
                    end
                end
            else
                @assert sys.mode in (:dipole, :dipole_uncorrected)
                data = swt.data::SWTDataDipole
                t = reshape(view(Tq, :, band), Na, 2)
                for i in 1:Na, Î¼ in 1:Nobs
                    O = data.observables_localized[Î¼, i]
                    # This is the Avec of the two transverse and one
                    # longitudinal directions in the local frame. (In the
                    # local frame, z is longitudinal, and we are computing
                    # the transverse part only, so the last entry is zero)
                    displacement_local_frame = SA[t[i, 2] + t[i, 1], im * (t[i, 2] - t[i, 1]), 0.0]
                    Avec[Î¼] += Avec_pref[Î¼, i] * (data.sqrtS[i]/âˆš2) * (O' * displacement_local_frame)[1]
                end
            end

            map!(corrbuf, measure.corr_pairs) do (Î¼, Î½)
                Avec[Î¼] * conj(Avec[Î½]) / Ncells
            end
            intensity[band, iq] = thermal_prefactor(disp[band, iq]; kT) * measure.combiner(q_global, corrbuf)
        end
    end

    disp = reshape(disp, L, size(qpts.qs)...)
    intensity = reshape(intensity, L, size(qpts.qs)...)
    return BandIntensities(cryst, qpts, disp, intensity)
end

"""
    intensities!(data, swt::SpinWaveTheory, qpts; energies, kernel, kT=0)
    intensities!(data, sc::SampledCorrelations, qpts; energies, kernel=nothing, kT=0)

Like [`intensities`](@ref), but makes use of storage space `data` to avoid
allocation costs.
"""
function intensities!(data, swt::AbstractSpinWaveTheory, qpts; energies, kernel::AbstractBroadening, kT=0)
    @assert size(data) == (length(energies), size(qpts.qs)...)
    bands = intensities_bands(swt, qpts; kT)
    @assert eltype(bands) == eltype(data)
    broaden!(data, bands; energies, kernel)
    return Intensities(bands.crystal, bands.qpts, collect(Float64, energies), data)
end

"""
    intensities(swt::SpinWaveTheory, qpts; energies, kernel, kT=0)
    intensities(sc::SampledCorrelations, qpts; energies, kernel=nothing, kT)

Calculates dynamical pair correlation intensities for a set of ``ðª``-points in
reciprocal space.

Linear spin wave theory calculations are performed with an instance of
[`SpinWaveTheory`](@ref). The alternative [`SpinWaveTheorySpiral`](@ref) allows
to study generalized spiral orders with a single, incommensurate-``ð¤`` ordering
wavevector. Another alternative [`SpinWaveTheoryKPM`](@ref) is favorable for
calculations on large magnetic cells, and allows to study systems with disorder.
An optional nonzero temperature `kT` will scale intensities by the quantum
thermal occupation factor ``|1 + n_B(Ï‰)|`` where ``n_B(Ï‰) = 1/(e^{Î²Ï‰}-1)`` is
the Bose function.

Intensities can also be calculated for `SampledCorrelations` associated with
classical spin dynamics. In this case, thermal broadening will already be
present, and the line-broadening `kernel` becomes optional. Conversely, the
parameter `kT` becomes required. If positive, it will introduce an intensity
correction factor ``|Î²Ï‰ (1 + n_B(Ï‰))|`` that undoes the occupation factor for
the classical Boltzmann distribution and applies the quantum thermal occupation
factor. The special choice `kT = nothing` will suppress the classical-to-quantum
correction factor, and yield statistics consistent with the classical Boltzmann
distribution.
"""
function intensities(swt::AbstractSpinWaveTheory, qpts; energies, kernel::AbstractBroadening, kT=0)
    return broaden(intensities_bands(swt, qpts; kT); energies, kernel)
end

"""
    intensities_static(swt::SpinWaveTheory, qpts; bounds=(-Inf, Inf), kernel=nothing, kT=0)
    intensities_static(sc::SampledCorrelations, qpts; bounds=(-Inf, Inf), kT)
    intensities_static(sc::SampledCorrelationsStatic, qpts)

Like [`intensities`](@ref), but integrates the dynamical correlations
``\\mathcal{S}(ðª, Ï‰)`` over a range of energies ``Ï‰``. By default, the
integration `bounds` are ``(-âˆž, âˆž)``, yielding the instantaneous (equal-time)
correlations.

In [`SpinWaveTheory`](@ref), the integral will be realized as a sum over
discrete bands. Alternative calculation methods are
[`SpinWaveTheorySpiral`](@ref) and [`SpinWaveTheoryKPM`](@ref).

Classical dynamics data in [`SampledCorrelations`](@ref) can also be used to
calculate static intensities. In this case, the domain of integration will be a
finite grid of available `energies`. Here, the parameter `kT` will be used to
account for the quantum thermal occupation of excitations, as documented in
[`intensities`](@ref).

Static intensities calculated from [`SampledCorrelationsStatic`](@ref) are
dynamics-independent. Instead, instantaneous correlations sampled from the
classical Boltzmann distribution will be reported.
"""
function intensities_static(swt::AbstractSpinWaveTheory, qpts; bounds=(-Inf, Inf), kernel=nothing, kT=0)
    res = intensities_bands(swt, qpts; kT)  # TODO: with_negative=true
    data_reduced = zeros(eltype(res.data), size(res.data)[2:end])
    for ib in axes(res.data, 1), iq in CartesianIndices(data_reduced)
        Ïµ = res.disp[ib, iq]
        if isnothing(kernel) || bounds == (-Inf, Inf)
            if bounds[1] <= Ïµ < bounds[2]
                data_reduced[iq] += res.data[ib, iq]
            end
        else
            isnothing(kernel.integral) && error("Kernel must provide integral")
            ihi = kernel.integral(bounds[2] - Ïµ)
            ilo = kernel.integral(bounds[1] - Ïµ)
            data_reduced[iq] += res.data[ib, iq] * (ihi - ilo)
        end
    end
    StaticIntensities(res.crystal, res.qpts, data_reduced)
end
